// https://github.com/Erkaman/vulkan_minimal_compute

#version 450
#extension GL_ARB_separate_shader_objects : enable

struct Pixel{
  vec4 value;
};

layout(set=0,std140, binding = 0) buffer buf
{
   Pixel imageData[];
};

layout(set=1,binding = 1) uniform ub {
  uint width;
  uint height;
} UBO;

void main() {

  /*
  In order to fit the work into workgroups, some unnecessary threads are launched.
  We terminate those threads here. 
  */
  //ubo.height = local_size_y;

  if(gl_GlobalInvocationID.x >= UBO.width || gl_GlobalInvocationID.y >= UBO.height)
    return;

  float x = float(gl_GlobalInvocationID.x) / float(UBO.width);
  float y = float(gl_GlobalInvocationID.y) / float(UBO.height);

  //ubo.width = max(gl_GlobalInvocationID.x, ubo.width);
  //ubo.height = max(gl_GlobalInvocationID.y, ubo.width);
  /*
  What follows is code for rendering the mandelbrot set. 
  */
  vec2 uv = vec2(x,y);
  float n = 0.0;
  vec2 c = vec2(-.445, 0.0) +  (uv - 0.5)*(2.0+ 1.7*0.2  ), 
  z = vec2(0.0);
  const int M =128;
  for (int i = 0; i<M; i++)
  {
    z = vec2(z.x*z.x - z.y*z.y, 2.*z.x*z.y) + c;
    if (dot(z, z) > 2) break;
    n++;
  }
          
  // we use a simple cosine palette to determine color:
  // http://iquilezles.org/www/articles/palettes/palettes.htm         
  float t = float(n) / float(M);
  vec3 d = vec3(0.3, 0.3 ,0.5);
  vec3 e = vec3(-0.2, -0.3 ,-0.5);
  vec3 f = vec3(2.1, 2.0, 3.0);
  vec3 g = vec3(0.0, 0.1, 0.0);
  vec4 color = vec4( d + e*cos( 6.28318*(f*t+g) ) ,1.0);
          
  // store the rendered mandelbrot set into a storage buffer:
  imageData[UBO.width * gl_GlobalInvocationID.y + gl_GlobalInvocationID.x].value = color;
}
